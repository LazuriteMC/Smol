plugins {
	id 'maven-publish'
	alias(libs.plugins.quilt.loom)
}

archivesBaseName = project.archives_base_name
version = project.version
group = project.maven_group

dependencies {
	minecraft libs.minecraft
	mappings loom.layered {
		addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:v2")
		officialMojangMappings()
	}

	modImplementation libs.quilt.loader
	modImplementation libs.quilted.fabric.api
}

processResources {
	inputs.property "version", version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}

	withSourcesJar()
}

jar.from 'LICENSE'

publishing {
	repositories {
		mavenLocal()

		maven {
			name 'Lazurite'
			url 'https://lazurite.dev/releases'

			credentials {
				username property('publish.lazurite.alias')
				password property('publish.lazurite.secret')
			}
		}
	}

	publications {
		rpgz MavenPublication, {
			from components.java
		}
	}
}